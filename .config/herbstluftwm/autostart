#!/usr/bin/zsh

# config for herbstluftwm

typeset -a cmds=()

function hc() { cmds+=("X" "$@") }

hc emit_hook reload

xsetroot -solid '#19202A'

# remove all existing keybindings
hc keyunbind --all

# default frame layout: 0 vertical | 1 horizontal | 2 max | 3 grid
hc set default_frame_layout horizontal 

# tags: 1 2 3 4 5 6 7 8 9
hc rename default "1" 2>/dev/null || true

# set default layout for 1st frame to horizontal
# default_frame_layout does not apply for this frame
hc load "1" '(clients horizontal:0)'

for tag in {2..9}
do
    hc add $tag

    # set default layout for tags "4", "5", "6" and "9" to max
    if [[ $tag =~ "4|5|6|9" ]]
    then
        hc load $tag '(clients max:0)'
    fi

done

# mouse
hc mouseunbind --all
hc mousebind Super-Button1 move
hc mousebind Super-Button2 zoom
hc mousebind Super-Button3 resize

# toggle tabs
#hc keybind $Mod-t set tabbed_max toggle
hc set tabbed_max off

# load theme
~/.config/herbstluftwm/theme

# load rules
~/.config/herbstluftwm/rules

hc set tree_style '╾│ ├└╼─┐'
hc set swap_monitors_to_get_tag 1

# set watch to trap layout changes for display in Polybar
hc watch tags.focus.tiling.focused_frame.algorithm

# multi monitor setup
# run hc detect_monitors to ensure that the original geometry is reset
# otherwise the padding on the primary monitor will "stack" when doing hc reload
herbstclient detect_monitors

typeset -a monitor_list
typeset -a rect
typeset -i polybar_height=$(polybar -q -d height)

for m in $(herbstclient list_monitors | cut -d: -f1)
do
    rect=( $(herbstclient monitor_rect $m) )

    if [[ $m -eq "0" ]]
    then
        rect[2]=$(( $polybar_height + 1 ))
        rect[4]=$(( ${rect[4]} - ${rect[2]} ))
    fi

    monitor_list+="${rect[3]}x${rect[4]}+${rect[1]}+${rect[2]}"
done

#monitor_list+="1690x1415+0+25"
#monitor_list+="870x1415+1690+25"

hc set_monitors $monitor_list

# unlock, just to be sure
hc unlock

echo ${cmds} > $HOME/.config/herbstluftwm/cmds

herbstclient chain ${cmds}

# start polybar panel and related scripts. Also starts dunst if it is not already running
~/.config/polybar/panel.sh &

# set focus to first monitor and position mouse top right (offset: 30px)
herbstclient focus_monitor 0
xpos=$(( $(herbstclient monitor_rect 0 | cut -d" " -f3) - 30 ))
#xpos=$(hc list_monitors | awk -F "x" '/0:/ { split($1,b," "); print b[2]-30 }')
xte "mousemove $xpos 30"

